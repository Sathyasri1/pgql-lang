module names

imports

  signatures/-

namespaces

  CommonPathExpression // regular PATH pattern
  Var

properties

  OriginOffset of Var

binding rules // scoping

  NormalizedQuery(_, _, _, _, _, _, _, _, _, _, _, _):
    scopes Var, CommonPathExpression

binding rules // variable definition

  CommonPathExpression(Identifier(p, _), _, _, _, _):
    defines unique CommonPathExpression p

  Vertex(Identifier(n, _), origin-offset, _):
    defines non-unique Var n
      of OriginOffset origin-offset
      of type VertexTy()

  Edge(_, Identifier(e, _), _, _, origin-offset, _):
    defines unique Var e
      of OriginOffset origin-offset
      of type EdgeTy()

  Path(_, _, _, _, Identifier(p, _), _, _, _, _):
    defines unique Var p
      of type PathTy()

  ExpAsVar(exp, Identifier(v, _), _, origin-offset):
    defines unique Var v // unlike SQL, we currently don't allow duplicate column names. This means that we eagerly detect potential ambiguous references). We may relax this in the future.
      of OriginOffset origin-offset

  VertexInsertion(Identifier(v, _), origin-offset, _, _):
    defines unique Var v
      of OriginOffset origin-offset
      of type VertexTy()

  DirectedEdgeInsertion(Identifier(v, _), origin-offset, _, _, _, _):
    defines unique Var v
      of OriginOffset origin-offset
      of type EdgeTy()

  OneRowPerVertex(Identifier(v, _), origin-offset, _):
    defines unique Var v
      of OriginOffset origin-offset
      of type VertexTy()

  OneRowPerEdge(Identifier(e, _), origin-offset, _):
    defines unique Var e
      of OriginOffset origin-offset
      of type EdgeTy()

  OneRowPerStep(Identifier(v1, _), origin-offset1, _, Identifier(e, _), origin-offset2, _, Identifier(v2, _), origin-offset3, _):
    defines unique Var v1
      of OriginOffset origin-offset1
      of type VertexTy()
    defines unique Var e
      of OriginOffset origin-offset2
      of type EdgeTy()
    defines unique Var v2
      of OriginOffset origin-offset3
      of type VertexTy()

binding rules // variable reference

  VarRef(Identifier(v, _), origin-offset):
    refers to Var v
      of OriginOffset origin-offset
